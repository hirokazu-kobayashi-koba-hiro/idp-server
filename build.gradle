plugins {
	id "com.diffplug.spotless" version "6.24.0"
}

task stage(dependsOn: ['clean',':app:bootJar']) {
	doLast {
		println "Build application..."
	}
}

subprojects {

	tasks.register('addApacheLicenseHeader') {
		group = 'license'
		description = "Add Apache 2.0 license header to all .java files in ${project.name}"

		doLast {
			def rootDirPath = project.projectDir.toPath()
			java.nio.file.Files.walk(rootDirPath)
					.filter { it.toString().endsWith('.java') }
					.forEach { file ->
						def content = file.toFile().getText('UTF-8')
						if (!content.contains('Licensed under the Apache License')) {
							println "Adding license to ${file.toAbsolutePath()}"
							def newContent = '''/**
 * Copyright 2025 Hirokazu Kobayashi
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 */''' + '\n\n' + content
							file.toFile().write(newContent, 'UTF-8')
						}
					}
		}
	}
}

tasks.register('addLicenseAll') {
	group = 'license'
	description = 'Run addApacheLicenseHeader on all subprojects'
	dependsOn subprojects.collect { it.tasks.named('addApacheLicenseHeader') }
}
