plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.2'
	id 'io.spring.dependency-management' version '1.1.4'
	id "com.diffplug.spotless" version "6.24.0"
}

group = 'org.idp.server'
version = '1.0.0'

repositories {
	mavenCentral()
}

dependencies {
	//idp-server-lib
	implementation project(':libs:idp-server-platform')
	implementation project(':libs:idp-server-core')
	implementation project(':libs:idp-server-control-plane')
	implementation project(':libs:idp-server-core-adapter')
	implementation project(':libs:idp-server-webauthn4j-adapter')
	implementation project(':libs:idp-server-notification-fcm-adapter')
	implementation project(':libs:idp-server-notification-apns-adapter')
	implementation project(':libs:idp-server-core-extension-ciba')
	implementation project(':libs:idp-server-core-extension-ida')
	implementation project(':libs:idp-server-authentication-interactors')
	implementation project(':libs:idp-server-core-extension-verifiable-credentials')
	implementation project(':libs:idp-server-security-event-framework')
	implementation project(':libs:idp-server-use-cases')

	//springboot
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	//session
	implementation 'org.springframework.session:spring-session-core'

	//webauthn
	implementation 'com.webauthn4j:webauthn4j-core:0.28.5.RELEASE'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation platform('org.junit:junit-bom:5.10.0')
	testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
	useJUnitPlatform()
}

jar {
	enabled = true
}

bootJar {
	enabled = false
}

spotless {
	java {
		target 'src/*/java/**/*.java'
		importOrder()
		removeUnusedImports()
		googleJavaFormat()
		toggleOffOn()
	}
	groovyGradle {
		target '*.gradle'
		greclipse()
	}
}